\subsection{Types}

\subsubsection{NaryOp}
\begin{minted}{scheme}
(claim NaryOp (-> Nat U U))
(define NaryOp
    (lambda (n a)
        (iter-Nat n a (lambda (u) (-> a u)))))
\end{minted}

\subsubsection{UNat (unary operator on Nat)}
\begin{minted}{scheme}
(claim UNat U)
(define UNat (NaryOp 1 Nat))
\end{minted}

\subsubsection{BinNat (binary operator on Nat)}
\begin{minted}{scheme}
(claim BinNat U)
(define BinNat (NaryOp 2 Nat))
\end{minted}


\subsection{Combinators}

\subsubsection{K (const)}
\begin{minted}{scheme}
(claim const
    (Pi ((a U) (b U)) (-> a b a)))
(define const
    (lambda (a b) (lambda (x y) x)))
\end{minted}

\subsubsection{I (identity)}
\begin{minted}{scheme}
(claim id 
    (Pi ((a U)) (-> a a)))
(define id
    (lambda (a) (lambda (x) x)))
\end{minted}

\subsubsection{C (flip)}
\begin{minted}{scheme}
(claim flip
    (Pi ((a U) (b U) (c U)) (-> (-> a b c) (-> b a c))))
(define flip
    (lambda (a b c)
        (lambda (f)
            (lambda (x y) (f y x)))))
\end{minted}

\subsubsection{B (compose)}
\begin{minted}{scheme}
(claim B
    (Pi ((a U) (b U) (c U)) (-> (-> b c) (-> a b) (-> a c))))
(define B
    (lambda (a b c)
        (lambda (f g)
            (lambda (x) (f (g x))))))
\end{minted}

\subsubsection{B2 (compose unary with binary)}
\begin{minted}{scheme}
(claim B2
    (Pi ((a U) (b U) (c U) (d U)) (-> (-> c d) (-> a b c) (-> a b d))))
;TODO: is it possible to use the B2 = BBB construction?
(define B2
    (lambda (a b c d)
        (lambda (f g)
            (lambda (x y) (f (g x y))))))
\end{minted}


\subsection{Functional Tools}

\subsubsection{iterate-n}
\begin{minted}{scheme}
(claim iterate-n
    (Pi ((a U)) (-> (-> a a) a Nat a)))
(define iterate-n
    (lambda (a)
        (lambda (f s n)
            (iter-Nat n s f))))
\end{minted}


\subsection{Arithmetic}

\subsubsection{successor}
\begin{minted}{scheme}
(claim succ UNat)
(define succ (lambda (n) (add1 n)))
\end{minted}

\subsubsection{predecessor}
\begin{minted}{scheme}
(claim pred UNat)
(define pred (lambda (n) (which-Nat n zero (id Nat))))
\end{minted}

\subsubsection{sgn (1 if n > 0, 0 otherwise)}
\begin{minted}{scheme}
(claim sgn UNat)
(define sgn (lambda (n) (which-Nat n zero ((const Nat Nat) 1))))
\end{minted}

\subsubsection{addition}
\begin{minted}{scheme}
(claim + BinNat)
(define + ((iterate-n Nat) succ))
\end{minted}

\subsubsection{subtraction}
\begin{minted}{scheme}
(claim - BinNat)
(define - ((iterate-n Nat) pred))
\end{minted}

\subsubsection{multiplication}
\begin{minted}{scheme}
(claim * BinNat)
(define * (lambda (n) ((iterate-n Nat) (+ n) 0)))
\end{minted}

\subsubsection{exponentiation}
\begin{minted}{scheme}
(claim ^ BinNat)
(define ^ (lambda (n m) ((iterate-n Nat) (* n) 1 m)))
\end{minted}

\subsubsection{floor div}
\begin{minted}{scheme}
(claim // BinNat)
(define //
    (lambda (n m)
        (cdr ((iterate-n (Pair Nat Nat)) 
            (lambda (p)
                (cons (- (car p) m) (+ (cdr p) (sgn (- (add1 (car p)) m))))) 
            (cons n 0)
            n))))
\end{minted}

\subsubsection{modulus}
\begin{minted}{scheme}
(claim % BinNat)
(define %
    (lambda (n m) (- n (* m (// n m)))))
\end{minted}

\subsubsection{lo (get exponent of factor)}
\begin{minted}{scheme}
(claim lo BinNat)
(define lo
    (lambda (b n)
        (cdr ((iterate-n (Pair Nat Nat))
                (lambda (p)
                    ((the (-> Nat Nat (Pair Nat Nat)) (lambda (i j) 
                        ((the (-> Nat (Pair Nat Nat)) (lambda (div?) 
                            (cons (* div? (// i b)) (+ j (* (sgn i) div?)))))
                         (- 1 (sgn (% i b))))))
                     (car p) (cdr p)))
                (cons n 0)
                n))))
\end{minted}


\subsection{Miscellaneous programs}

\subsubsection{Fibonacci}
\begin{minted}{scheme}
(claim __fib-helper-g UNat)
(define __fib-helper-g
    (lambda (n+1)
        (rec-Nat n+1
            6
            (lambda (n g-n) 
                (*
                    (^ 2 (lo 3 g-n))
                    (^ 3
                       (+ (lo 3 g-n) (lo 2 g-n))))))))

(claim fib UNat)
(define fib (lambda (n) (lo 2 (__fib-helper-g n))))
\end{minted}


