#lang pie

;;;;;;;;;;;;;;;
;@begin section Types
;;;;;;;;;;;;;;;

;@begin code NaryOp
(claim NaryOp (-> Nat U U))
(define NaryOp
    (lambda (n a)
        (iter-Nat n a (lambda (u) (-> a u)))))
;@end

;@begin code UNat (unary operator on Nat)
(claim UNat U)
(define UNat (NaryOp 1 Nat))
;@end

;@begin code BinNat (binary operator on Nat)
(claim BinNat U)
(define BinNat (NaryOp 2 Nat))
;@end

;@end // section Types

;;;;;;;;;;;;;;;;;;;;;
;@begin section Combinators
;;;;;;;;;;;;;;;;;;;;;

;@begin code K (const)
(claim const
    (Pi ((a U) (b U)) (-> a b a)))
(define const
    (lambda (a b) (lambda (x y) x)))
;@end

;@begin code I (identity)
(claim id 
    (Pi ((a U)) (-> a a)))
(define id
    (lambda (a) (lambda (x) x)))
;@end

;@begin code C (flip)
(claim flip
    (Pi ((a U) (b U) (c U)) (-> (-> a b c) (-> b a c))))
(define flip
    (lambda (a b c)
        (lambda (f)
            (lambda (x y) (f y x)))))
;@end

;@begin code B (compose)
(claim B
    (Pi ((a U) (b U) (c U)) (-> (-> b c) (-> a b) (-> a c))))
(define B
    (lambda (a b c)
        (lambda (f g)
            (lambda (x) (f (g x))))))
;@end

;@begin code B2 (compose unary with binary)
(claim B2
    (Pi ((a U) (b U) (c U) (d U)) (-> (-> c d) (-> a b c) (-> a b d))))
;TODO: is it possible to use the B2 = BBB construction?
(define B2
    (lambda (a b c d)
        (lambda (f g)
            (lambda (x y) (f (g x y))))))
;@end

;@end // section Combinators

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;@begin section Functional Tools
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;@begin code iterate-n
(claim iterate-n
    (Pi ((a U)) (-> (-> a a) a Nat a)))
(define iterate-n
    (lambda (a)
        (lambda (f s n)
            (iter-Nat n s f))))
;@end

;@end // section Functional Tools

;;;;;;;;;;;;;;;;;;;;
;@begin section Arithmetic
;;;;;;;;;;;;;;;;;;;;

;@begin code successor
(claim succ UNat)
(define succ (lambda (n) (add1 n)))
;@end

;@begin code predecessor
(claim pred UNat)
(define pred (lambda (n) (which-Nat n zero (id Nat))))
;@end

;@begin code sgn (1 if n > 0, 0 otherwise)
(claim sgn UNat)
(define sgn (lambda (n) (which-Nat n zero ((const Nat Nat) 1))))
;@end

;@begin code addition
(claim + BinNat)
(define + ((iterate-n Nat) succ))
;@end

;@begin code subtraction
(claim - BinNat)
(define - ((iterate-n Nat) pred))
;@end

;@begin code multiplication
(claim * BinNat)
(define * (lambda (n) ((iterate-n Nat) (+ n) 0)))
;@end

;@begin code exponentiation
(claim ^ BinNat)
(define ^ (lambda (n m) ((iterate-n Nat) (* n) 1 m)))
;@end

;@begin code floor div
(claim // BinNat)
(define //
    (lambda (n m)
        (cdr ((iterate-n (Pair Nat Nat)) 
            (lambda (p)
                (cons (- (car p) m) (+ (cdr p) (sgn (- (add1 (car p)) m))))) 
            (cons n 0)
            n))))
;@end

;@begin code modulus
(claim % BinNat)
(define %
    (lambda (n m) (- n (* m (// n m)))))
;@end

;@begin code lo (get exponent of factor)
(claim lo BinNat)
(define lo
    (lambda (b n)
        (cdr ((iterate-n (Pair Nat Nat))
                (lambda (p)
                    ((the (-> Nat Nat (Pair Nat Nat)) (lambda (i j) 
                        ((the (-> Nat (Pair Nat Nat)) (lambda (div?) 
                            (cons (* div? (// i b)) (+ j (* (sgn i) div?)))))
                         (- 1 (sgn (% i b))))))
                     (car p) (cdr p)))
                (cons n 0)
                n))))
;@end

;@end // section Arithmetic

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;@begin section Miscellaneous programs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;@begin code Fibonacci
(claim __fib-helper-g UNat)
(define __fib-helper-g
    (lambda (n+1)
        (rec-Nat n+1
            6
            (lambda (n g-n) 
                (*
                    (^ 2 (lo 3 g-n))
                    (^ 3
                       (+ (lo 3 g-n) (lo 2 g-n))))))))

(claim fib UNat)
(define fib (lambda (n) (lo 2 (__fib-helper-g n))))
;@end

;@end // section Miscellaneous programs